options
{
	LOOKAHEAD=2;
	FORCE_LA_CHECK=true;
}

PARSER_BEGIN(CFlow)
package parser;

import java.io.StringReader;

import automata.NFA;
import automata.DFA;
import generator.CFlowGenerator;

public class CFlow {
	public static void main(String args[]) throws ParseException {
		System.setProperty("org.graphstream.ui.renderer", "org.graphstream.ui.j2dviewer.J2DGraphRenderer");

		String regex = args[0];	
		StringReader reader = new StringReader(regex);

		String outputDir = args[1];;

		CFlow cflow = new CFlow(reader);
		SimpleNode root = cflow.RegExp();
		// root.dump("");

		NFA nfa = NFA.init(root);
		// nfa.display();
		DFA dfa = new DFA(nfa);
		// dfa.display();

		try {
			CFlowGenerator gen = new CFlowGenerator(dfa, outputDir);
			gen.generate();
		} catch (Exception e) {
			e.printStackTrace();
		}
	}
}
	
PARSER_END(CFlow)

SKIP:
{
	" "
	|
	"\t"
	|
	"\r"
}

TOKEN:
{
	<LINESTART: "^">
	|
	<LINEEND: "$">
	|
	<OR: "|">
	|
	<QUANTIFIER: "?">
	|
	<OPEN_GROUP: "(">
	|
	<CLOSE_GROUP: ")">
	|
	<OPEN_SET: "[">
	|
	<CLOSE_SET: "]">
	|
	<OPEN_REPEAT: "{">
	|
	<CLOSE_REPEAT: "}">
	|
	<KLEEN_CLOSURE: "*">
	|
	<PLUS: "+">
	|
	<CHARACTER: (["A"-"Z"] | ["a"-"z"])>
	|
	<NUMBER: ["0"-"9"]>
	|
	<ANY: ".">
	|
  	<LF: "\n">
}

SimpleNode RegExp() #Expression:
{
}
{
	Expr() [Union()
	{
		jjtThis.value = "|";
	}
	#Union(2)]
	{
	    return jjtThis;
	}
}

void Union() #void: {}
{
	<OR> RegExp()
}

void Expr() #void: {}
{
	BasicExp() [Expr() #Concat(2)]
}

void BasicExp() #void:
{
	Token t;
}
{
	ElementaryRegex() [(t = "*" | t = "+" | t = "?")
	{
		jjtThis.value = t.image;
	}
	#Repeat(1)]
}

void ElementaryRegex() #void: {}
{
	Group()
	|
	<ANY>
	|
	<LINEEND>
	|
	Set()
	|
	Char()
}

void Char():
{
	Token t1, t2;
}
{
	t1 = <NUMBER>
	{
		jjtThis.value = t1.image;
	}
	|
	t2 = <CHARACTER>
	{
		jjtThis.value = t2.image;
	}
}

void Group() #void: {}
{
	"(" RegExp() ")"
}

void Set() #void: {}
{
	PositiveSet()
	|
	NegativeSet()
}

void PositiveSet(): {}
{
	"[" SetItems() "]"
}

void NegativeSet(): {}
{
	"[^" SetItems() "]"
}

void SetItems() #void: {}
{
	SetItem() [SetItems()]
}

void SetItem() #void: {}
{
	Range() 
	|
	Char()
}

void Range(): {}
{
	Char() "-" Char()
}